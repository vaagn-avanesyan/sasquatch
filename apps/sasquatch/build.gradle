/*
 * Copyright (c) Microsoft Corporation. All rights reserved.
 * Licensed under the MIT License.
 */

//
// :apps:sasquatch
//

evaluationDependsOn(':apps')

android {

    flavorDimensions "dependency", "pushLibrary"

    defaultConfig {
        externalNativeBuild {
            ndkBuild {
                arguments "NDK_APPLICATION_MK=Application.mk", "V=1"
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    productFlavors {
        projectDependency {
            dimension "dependency"
            applicationIdSuffix ".project"
        }
        jcenterDependency {
            dimension "dependency"
            applicationIdSuffix ".jcenter"
        }
        vanilla {
            dimension "pushLibrary"
        }
        firebase {
            dimension "pushLibrary"
        }
    }

    buildTypes {
        releaseProguard {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "Android.mk"
        }
    }
}

repositories {
    maven {
        url "https://dl.bintray.com/vsappcenter/appcenter-snapshot"
    }
}

dependencies {
    implementation "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:customtabs:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"

    projectDependencyImplementation project(':sdk:appcenter-analytics')
    projectDependencyImplementation project(':sdk:appcenter-crashes')
    projectDependencyImplementation project(':sdk:appcenter-distribute')
    projectDependencyImplementation project(':sdk:appcenter-push')
    projectDependencyImplementation project(':sdk:appcenter-rum')
    projectDependencyImplementation project(':sdk:appcenter-auth')
    projectDependencyImplementation project(':sdk:appcenter-data')

    def appCenterSdkVersion = "2.2.0"
    jcenterDependencyImplementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    jcenterDependencyImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    jcenterDependencyImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    jcenterDependencyImplementation "com.microsoft.appcenter:appcenter-push:${appCenterSdkVersion}"
    jcenterDependencyImplementation "com.microsoft.appcenter:appcenter-auth:${appCenterSdkVersion}"
    jcenterDependencyImplementation "com.microsoft.appcenter:appcenter-data:${appCenterSdkVersion}"

    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
}

// We are interested in excluding the plugin only for the vanilla sasquatch build variant.
def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
if (!taskRequests.contains("sasquatch") || taskRequests.contains("Firebase")) {
    apply plugin: 'com.google.gms.google-services'
}

/*
 * Project and jcenter conflict since they are not using same version of Push and
 * somehow affects the plugin when building.
 * If we build project alone (commenting jcenter dependencies), the problem is solved.
 * So until we release the SDK with the push update, disable version check.
 * TODO remove this after SDK release.
 */
//noinspection UnnecessaryQualifiedReference
com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true
